#version 450 core

layout(local_size_x = 32, local_size_y = 32, local_size_z = 1) in;

layout (set = 0, binding = 0, rgba8) uniform image2D texture;

layout(push_constant, std140) uniform ComputeInfo {
    int level;
    int block_dim;
};

void perform_haar_transform(int dim) {
    int off = int(pow(block_dim, level));
    const int block_offset = int(pow(block_dim, level + 1));
    int p_offset = dim >> 1;
    for (int y = 0; y < block_dim * off; y += dim) {
        for (int x = 0; x < block_dim * off; x += dim) {
            ivec2 coord = ivec2(gl_GlobalInvocationID.xy) * block_offset + ivec2(x, y);

            // Load the pixels in the current dimXdim block.
            vec4 a = imageLoad(texture, coord);
            vec4 b = imageLoad(texture, coord + ivec2(p_offset, 0));
            vec4 c = imageLoad(texture, coord + ivec2(0, p_offset));
            vec4 d = imageLoad(texture, coord + ivec2(p_offset, p_offset));

            // Compute averages between the pixels.
            vec4 ll = (a + b + c + d) / 4.0;
            vec4 lh = (a - b + c - d) / 4.0;
            vec4 hl = (a + b - c - d) / 4.0;
            vec4 hh = (a - b - c + d) / 4.0;
            imageStore(texture, coord, ll);
            imageStore(texture, coord + ivec2(p_offset, 0), lh);
            imageStore(texture, coord + ivec2(0, p_offset), hl);
            imageStore(texture, coord + ivec2(p_offset, p_offset), hh);
        }
    }
}

void haar_block_x_axis(int dim, int offset) {
    const int block_offset = offset * block_dim;
    const int p_offset = dim >> 1;
    for (int y = 0; y < block_offset; y++) {
        for (int x = 0; x < block_offset; x += dim) {
            ivec2 coord = ivec2(gl_GlobalInvocationID.xy) * block_offset + ivec2(x, y);

            // Load the pixels in the current dimXdim block.
            vec4 a = imageLoad(texture, coord);
            vec4 b = imageLoad(texture, coord + ivec2(p_offset, 0));

            // Compute averages between the pixels.
            vec4 lh = b - a;
            vec4 ll = a + (lh / 2.0) + (1.0 / 512.0);
            imageStore(texture, coord, ll);
            imageStore(texture, coord + ivec2(p_offset, 0), lh);
        }
    }
}

void haar_block_y_axis(int dim, int offset) {
    const int block_offset = offset * block_dim;
    const int p_offset = dim >> 1;
    for (int x = 0; x < block_offset; x++) {
        for (int y = 0; y < block_offset; y += dim) {
            ivec2 coord = ivec2(gl_GlobalInvocationID.xy) * block_offset + ivec2(x, y);

            // Load the pixels in the current dimXdim block.
            vec4 a = imageLoad(texture, coord);
            vec4 b = imageLoad(texture, coord + ivec2(0, p_offset));

            // Compute averages between the pixels.
            vec4 lh = b - a;
            vec4 ll = a + (lh / 2.0) + (1.0 / 512.0);
            imageStore(texture, coord, ll);
            imageStore(texture, coord + ivec2(0, p_offset), lh);
        }
    }
}

void main() {
    int offset = int(pow(block_dim, level));
    for (int i = 2 * offset; i <= block_dim * offset; i <<= 1) {
        haar_block_x_axis(i, offset);
    }
    for (int i = 2 * offset; i <= block_dim * offset; i <<= 1) {
        haar_block_y_axis(i, offset);
    }
}
